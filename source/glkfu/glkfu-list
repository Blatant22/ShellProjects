#!/usr/bin/env perl
#cito M:755 O:0 G:0 T:/usr/bin/glkfu-list
#------------------------------------------------------------------------------
# Project Name      - Extra/source/glkfu/glkfu-list
# Started On        - Sun 20 Dec 03:09:11 GMT 2020
# Last Change       - Thu 24 Dec 01:53:40 GMT 2020
# Author E-Mail     - terminalforlife@yahoo.com
# Author GitHub     - https://github.com/terminalforlife
#------------------------------------------------------------------------------
# Very basic complimentary tool to GLKFU to simply list out all of the
# available XZ tarballs stored on kernel.org, whether they're stable or not.
#
# Dependencies:
#
#   glkfu (>= 2020-12-20)
#   libsort-versions-perl (>= 1.62-1)
#   libwww-perl (>= 6.31-1)
#   perl (>= 5.22.1-9)
#------------------------------------------------------------------------------

use strict;
use warnings;
use autodie;
use LWP::Simple;
use Sort::Versions 'versioncmp';

no warnings 'uninitialized';

my $CurVer = '2020-12-24';
my $Progrm = ($0 =~ m{(?:.*/)?([^/].*)})[0];

sub Usage {
	print(qq{Usage: $Progrm [OPTS]

		  -h, --help               - Display this help information.
		  -v, --version            - Output the version datestamp.
		  -C, --no-color           - Disable ANSI color escape sequences.

		  The current version is by default highlighted in green, if possible.
	} =~ tr/\t//dr)
}

my $DoColor = 'True';
my $Domain = 'https://www.kernel.org';

while (defined($ARGV[0])) {
	if ($ARGV[0] =~ '^(--help|-h)$') {
		Usage(); exit(0)
	} elsif ($ARGV[0] =~ '^(--version|-v)$') {
		print("$CurVer\n"); exit(0)
	} elsif ($ARGV[0] =~ '^(--no-color|-C)$') {
		$DoColor = 'False'
	} else {
		die("Incorrect option(s) specified")
	}

	shift()
}

my @Data = split("\n", get("$Domain/pub/linux/kernel"));

my @KernVers;
foreach (@Data) {
	my $File = (m{(?:.*>)?(.*)?(?:<.*)})[0];
	push(@KernVers, $File) if $File =~ '^v\d+\.[\dx]+/'
}

my (@Tarballs, @Versions);
foreach my $Version (@KernVers) {
	@Data = split("\n", get("$Domain/pub/linux/kernel/$Version"));

	foreach (@Data) {
		my $File = (m{(?:.*>)?(.*)?(?:<.*)})[0];
		next unless $File =~ '^linux-[\d\.]+.tar.xz$';
		push(@Versions, (m{(?:linux-)([\d\.]+)(?:\.tar\.xz)})[0])
	}
}

chomp(my $CurKernVer = `glkfu -o current`);
foreach my $Version (sort({versioncmp($a, $b)} @Versions)) {
	if ($Version eq $CurKernVer and $DoColor eq 'True') {
		printf("\e[1;32mlinux-%s.tar.xz\e[0m\n", $Version)
	} else {
		printf("linux-%s.tar.xz\n", $Version)
	}
}
